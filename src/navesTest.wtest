import naves.*

/* Para pensar: ¿Hace falta testear más casos? 
 * 
 * Respuesta: No, no hace falta, ya que encontrarseConEnemigo() produce otros efectos ya testeados.
 * Con solo testear que se cumplan ambos efectos para un caso, es suficiente para demostrar que se 
 * cumple para todos.
 * 
 * */

describe "Nave de carga" {

	const naveVacia = new NaveDeCarga()
	const naveLlena = new NaveDeCarga(carga = 500000, velocidad = 290000)

	test "sobrecargada" {
		assert.that(naveLlena.sobrecargada())
	}

	test "no sobrecargada" {
		assert.notThat(naveVacia.sobrecargada())
	}

	test "excedida de velocidad" {
		assert.that(naveLlena.excedidaDeVelocidad())
	}

	test "no excedida de velocidad" {
		assert.notThat(naveVacia.excedidaDeVelocidad())
	}

	test "recibir amenaza" {
		naveLlena.recibirAmenaza()
		assert.equals(0, naveVacia.carga())
	}
	
	test "propulsar sin tener velocidad" {
		naveVacia.propulsar()	
		assert.equals(20000, naveVacia.velocidad())
	}
	
	test "propulsar alcanzando máximo de velocidad" {
		naveLlena.propulsar()
		assert.equals(300000, naveLlena.velocidad())
	}
	
}

describe "Nave de residuos" {
	const naveSellada = new NaveDeResiduos(carga = 5000, velocidad = 10000, selladoAlVacio = true)
	const naveSinSellar = new NaveDeResiduos(selladoAlVacio = false)
	
	test "recibir amenaza" {
		naveSellada.recibirAmenaza()
		assert.equals(5000, naveSellada.carga())
		assert.equals(0, naveSellada.velocidad())
	}
	
	test "prepararse para viaje" {
		naveSinSellar.prepararParaViajar()
		assert.equals(15000, naveSinSellar.velocidad())
		assert.that(naveSinSellar.selladoAlVacio())
	}
}

describe "Nave de pasajeros" {

	const naveSinPasajeros = new NaveDePasajeros()
	const naveConPasajeros = new NaveDePasajeros(cantidadDePasajeros = 296, velocidad = 290000)

	test "velocidad maxima sin pasajeros" {
		assert.equals(75000, naveSinPasajeros.velocidadMaximaLegal())
	}

	test "velocidad maxima con pasajeros" {
		assert.equals(1000 - 200, naveConPasajeros.velocidadMaximaLegal())
	}

	test "esta en peligro" {
		assert.that(naveConPasajeros.estaEnPeligro())
	}

	test "no esta en peligro" {
		assert.notThat(naveSinPasajeros.estaEnPeligro())
	}

	test "recibir amenaza" {
		naveSinPasajeros.recibirAmenaza()
		assert.that(naveSinPasajeros.alarma())
	}
	
	test "preparar para viajar" {
		naveConPasajeros.prepararParaViajar()
		assert.equals(300000, naveConPasajeros.velocidad())
	}
	
	test "encontrarse con enemigo" {
		naveSinPasajeros.encontrarseConEnemigo()
		assert.equals(20000, naveSinPasajeros.velocidad())
		assert.that(naveSinPasajeros.alarma())
	}

}

describe "Nave de combate" {

	const naveEnReposo = new NaveDeCombate()
	const naveEnAtaque = new NaveDeCombate(modo = ataque)

	test "no esta invisible en reposo" {
		assert.notThat(naveEnReposo.estaInvisible())
	}

	test "esta invisible en ataque" {
		assert.that(naveEnAtaque.estaInvisible())
	}

	test "no esta invisible en ataque" {
		naveEnAtaque.velocidad(20000)
		assert.notThat(naveEnAtaque.estaInvisible())
	}

	test "recibir amenaza en reposo" {
		naveEnReposo.recibirAmenaza()
		assert.equals("¡RETIRADA!", naveEnReposo.ultimoMensaje())
	}

	test "recibir amenaza en ataque" {
		naveEnAtaque.recibirAmenaza()
		assert.equals("Enemigo encontrado", naveEnAtaque.ultimoMensaje())
	}
	
	test "prepararse para viaje en ataque" {
		naveEnAtaque.prepararParaViajar()
		assert.equals("Volviendo a la base", naveEnAtaque.ultimoMensaje())
		assert.equals(15000, naveEnAtaque.velocidad())
	}
	
	test "prepararse para viaje en reposo" {
		naveEnReposo.prepararParaViajar()
		assert.equals("Saliendo en misión", naveEnReposo.ultimoMensaje())
		assert.equals(15000, naveEnReposo.velocidad())
		assert.equals(ataque, naveEnReposo.modo())
	}
}

